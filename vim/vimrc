" vim: set sw=2 ts=2 sts=2 et tw=78 spell

if !1 | finish | endif
if !&compatible | set nocompatible | endif

augroup MyAutoCmd
  autocmd!
augroup END

let s:is_windows = has('win16') || has('win32') || has('win64')
let s:is_cygwin = has('win32unix')
function! IsWindows()
  return s:is_windows
endfunction
function! IsMac()
  return !s:is_windows && !s:is_cygwin
        \ && (has('mac') || has('macunix') || has('gui_macvim') ||
        \   (!executable('xdg-open') &&
        \     system('uname') =~? '^darwin'))
endfunction

let $CACHE = expand('~/.vim/.cache')
if !isdirectory(expand($CACHE))
  call mkdir(expand($CACHE), 'p')
endif

set encoding=utf-8      "Sets the character encoding used inside Vim
set termencoding=utf-8
set fileencoding=utf-8
set fileencodings=ucs-bom,utf-8,gb2312,cp936,chinese,latin1
language messages en_US

if has('vim_starting')
  if IsWindows()
    let &runtimepath = join([
          \ expand('~/.vim'),
          \ expand('$VIMRUNTIME'),
          \ expand('~/.vim/after')], ',')
  endif
endif

if !isdirectory(expand('~/.vim/autoload'))
  silent call mkdir(expand('~/.vim/autoload'), 'p')
endif

if !isdirectory(expand('~/.vim/bundle'))
  silent call mkdir(expand('~/.vim/bundle'), 'p')
endif

if finddir('Vundle.vim', expand('~/.vim/bundle')) == ''
  echon 'Installing Vundle...'
  silent !git clone https://github.com/VundleVim/Vundle.vim.git $HOME/.vim/bundle/Vundle.vim
  echo 'done.'
  if v:shell_error
    echoerr 'Vundle installation has failed!'
    finish
  endif
endif

set rtp+=~/.vim/bundle/Vundle.vim

call vundle#begin(expand('~/.vim/bundle'))

Plugin 'VundleVim/Vundle.vim'
Plugin 'altercation/vim-colors-solarized'
Plugin 'tomasr/molokai'
Plugin 'bling/vim-airline'
Plugin 'Lokaltog/vim-easymotion'
Plugin 'Raimondi/delimitMate'
Plugin 'tpope/vim-surround'
Plugin 'tpope/vim-repeat'
Plugin 'matchit.zip'
Plugin 'xolox/vim-misc'
Plugin 'xolox/vim-session'
Plugin 'scrooloose/syntastic'
Plugin 'majutsushi/tagbar'
Plugin 'ctrlpvim/ctrlp.vim'
Plugin 'tacahiroy/ctrlp-funky'
Plugin 'mileszs/ack.vim'
"Plugin 'SirVer/ultisnips'
"Plugin 'honza/vim-snippets'
"Plugin 'Valloric/YouCompleteMe'
" Plugin 'marijnh/tern_for_vim'
" Plugin 'fatih/vim-go'
" Plugin 'tpope/vim-rails'
" Plugin 'tpope/vim-bundler'
" Plugin 'tpope/vim-rake'
" Plugin 'mattn/emmet-vim'
" Plugin 'pangloss/vim-javascript'
" Plugin 'kchmck/vim-coffee-script'

call vundle#end()

filetype plugin indent on     " Enable detection, plugins and indenting in one step
if exists('&ambiwidth')
  set ambiwidth=double        "Use twice the width of ASCII characters for Multibyte
endif
set autoread                  "Automatically read file again which has been changed outside of Vim
set backspace=indent,eol,start "Working of <BS>,<Del>,CTRL-W,CTRL-U
if has('unnamedplus')
  set clipboard& clipboard=unnamedplus      "uses the clipboard register '+'
else
  set clipboard& clipboard+=unnamed,autoselect
endif
"set cmdheight=1        "Number of screen lines to use for the command-line
"set cmdwinheight=5     "Number of screen lines to use for the command-line window
set display=lastline    "Display as much as possible of the last line
set diffopt+=iwhite
set formatoptions-=r,o  "Turn off Automatically comment out when line break
" If we have Vim 7.4, add j to the format options to get rid of comment
" leaders when joining lines
if v:version >= 704
    set formatoptions+=j
endif
set grepprg=internal          "Program to use for the :grep command
"set helpheight=12            "Minimal initial height of the help window
set helplang& helplang=en,zh  "If true Vim master, use English help file
set hidden                    "Display another buffer when current buffer isn't saved.
set history=1024              "Amount of Command history
set infercase                 "Ignore case on insert completion.
set keywordprg=:help          "Open Vim internal help by K command
set laststatus=2              "Always display statusline
set matchpairs& matchpairs+=<:> "Characters that form pairs
set matchtime=1                 "Tenths of a second to show the matching paren
set modeline                    "Set Vim local buffer option to specific file
set noerrorbells                "Don't ring the bell for error messages
set novisualbell                "Don't use visual bell instead of beeping
set nrformats-=octal            "Bases Vim will consider for numbers(Ctrl-a,Ctrl-x)
set number                      "Print the line number in front of each line
set ruler                       "Show the line and column number of the cursor position
set shortmess& shortmess+=I     "Don't give the message when starting Vim :intro
set showcmd                     "Show (partial) command in the last line of the screen
set showmatch                   "Briefly jump to the matching one
set nospell                     "Spell checking on
set spelllang=en                "Spell checking language
set textwidth=0                 "Maximum width of text that is being inserted
set viewoptions=cursor,folds    "Changes the effect of the :mkview command
set viminfo+=!                  "Store information when you exit Vim for later
set virtualedit=block           "Cursor can be positioned virtually when Visual-block mode
set whichwrap=b,s,h,l,[,],<,>   "Allow specified keys to move to the previous/next line
set wrap                        "Lines longer than the width of the window will wrap
set wrapscan                    "Searches wrap around the end of the file
set mouse=a                     "Enable mouse
set showmode                    "Display the current mode
"set cursorline                 "Highlight current line
set winaltkeys=no               "Turns off the Alt key bindings to the gui menu
set timeoutlen=3000             "Keymapping timeout
set ttimeoutlen=100
set splitright                  "Puts new vsplit windows to the right of the current
set splitbelow                  "Puts new split windows to the bottom of the current
set fileformat=unix             "Default fileformat
set fileformats=unix,dos,mac    "This gives the end-of-line (<EOL>) formats
set autoindent                  "Copy indent from current line when starting a new line
set expandtab                   "Use the appropriate number of spaces to insert a <Tab>
set shiftround                  "Round indent to multiple of 'shiftwidth'
set shiftwidth=2                "Number of spaces to use for each step of (auto)indent
set softtabstop=2               "Number of spaces that a <Tab> counts for while editing operations
set tabstop=2                   "Number of spaces that a <Tab> in the file counts for
set incsearch                   "Incremental searching
set ignorecase                  "Ignore case in search patterns
set smartcase                   "Override the ignorecase option if the pattern contains upper case
set hlsearch | nohlsearch "Highlight search patterns, support reloading

if ! isdirectory(expand($CACHE.'/vimbackup'))
  call mkdir(expand($CACHE.'/vimbackup'), 'p')
endif
set backup                      "Make a backup before overwriting a file
set writebackup                 "Make a backup before overwriting a file
set backupdir=$CACHE/vimbackup  "List of directories for the backup file
"Make backup files like you are millionaire (in terms disk usage resourses)
autocmd MyAutoCmd BufWritePre * let &backupext = '_' . strftime('%Y%m%d_%X') . '~'

if ! isdirectory(expand($CACHE.'/vimswap'))
  call mkdir(expand($CACHE.'/vimswap', 'p'))
endif
set swapfile                    "Use a swapfile for the buffer
set directory=$CACHE/vimswap

if has('persistent_undo')
  if ! isdirectory(expand($CACHE.'/vimundo'))
    call mkdir(expand($CACHE.'/vimundo', 'p'))
  endif
  set undofile                  "Automatically saves undo history
  set undoreload=1000           "Save the whole buffer for undo when reloading it
  set undodir=$CACHE/vimundo
  set undofile
endif

set wildmenu                    "Command line autocompletion
set wildmode=list:longest,full  "Shows all the options

set wildignore&                 "A file that matches with one of these patterns is ignored
set wildignore+=*.sw?                            " Vim swap files
set wildignore+=*.bak,*.?~,*.??~,*.???~,*.~      " Backup files
set wildignore+=*.luac                           " Lua byte code
set wildignore+=*.jar                            " Java archives
set wildignore+=*.pyc                            " Python byte code
set wildignore+=*.stats                          " Pylint stats

set foldenable      "Enable fold
set foldlevel=100   "Folds with a higher level will be closed

if has('vim_starting')
  syntax enable
  set background=dark
  set t_Co=256
  if &t_Co < 256
    colorscheme default
  else
    try
      colorscheme solarized
    catch
      colorscheme desert
    endtry
  endif
endif

set guioptions-=T           "Remove the toolbar
set lines=40                "40 lines of text instead of 24
if IsWindows()
  set guifont=Andale_Mono:h11,Menlo:h11,Consolas:h13,Courier_New:h11
elseif IsMac()
  set guifont=Consolas:h13,Andale\ Mono\ Regular:h12,Menlo\ Regular:h11,Consolas\ Regular:h12,Courier\ New\ Regular:h14
else
  set guifont=Andale\ Mono\ Regular\ 12,Menlo\ Regular\ 11,Consolas\ Regular\ 12,Courier\ New\ Regular\ 14
endif


command! EVimrc e $MYVIMRC
command! ETabVimrc tabnew $MYVIMRC
command! SoVimrc source $MYVIMRC
autocmd MyAutoCmd BufWritePost *vimrc source $MYVIMRC
autocmd MyAutoCmd BufWritePost *gvimrc if has('gui_running') source $MYGVIMRC

let g:mapleader = ','
nnoremap ,  <Nop>
xnoremap ,  <Nop>

nnoremap <CR> o<ESC>

nnoremap <ESC><ESC> :nohlsearch<CR>:match<CR>

nnoremap j gj
vnoremap j gj
nnoremap gj j
vnoremap gj j

nnoremap k gk
vnoremap k gk
nnoremap gk k
vnoremap gk k

nnoremap <C-e> <C-e>j
nnoremap <C-y> <C-y>k
nnoremap <C-f> <C-f>zz
nnoremap <C-b> <C-b>zz

nnoremap <Space>j <C-f>zz

nnoremap <Space>k <C-b>zz
vnoremap <Space>j <C-f>zz
vnoremap <Space>k <C-b>zz

nnoremap Y y$

autocmd MyAutoCmd InsertLeave * set nopaste

set listchars=tab:â–¸\ ,
set list

function! s:remove_trailing_white_spaces()
  let pos = winsaveview()
  silent! execute '%s/\s\+$//g'
  call winrestview(pos)
endfunction
command! RemoveTrailingWhiteSpaces call <SID>remove_trailing_white_spaces()
command! -range=% TrimSpace  <line1>,<line2>s!\s*$!!g | nohlsearch
" remove trail ^M
command! -range=% RemoveTrailM  <line1>,<line2>s!\r$!!g | nohlsearch
"}}}

" Restore last cursor position when open a file
autocmd MyAutoCmd BufReadPost * if line("'\"") > 1 && line("'\"") <= line("$") | exe "normal! g`\"" | endif

" Create Directory Automatically
autocmd MyAutoCmd BufWritePre * call s:auto_mkdir(expand('<afile>:p:h'), v:cmdbang)
function! s:auto_mkdir(dir, force)
  if !isdirectory(a:dir) && (a:force ||
  \  input(printf('"%s" does not exist. Create? [y/N]', a:dir)) =~? '^y\%[es]$')
    call mkdir(iconv(a:dir, &encoding, &termencoding), 'p')
  endif
endfunction

autocmd MyAutoCmd BufRead,BufNewFile,BufReadPre *.coffee   setlocal filetype=coffee
autocmd MyAutoCmd FileType coffee     setlocal sw=2 sts=2 ts=2 et
autocmd MyAutoCmd BufWritePost,FileWritePost *.coffee silent CoffeeMake! -cb | cwindow | redraw!

autocmd MyAutoCmd BufRead,BufNewFile *.md  set filetype=markdown
autocmd MyAutoCmd FileType markdown setlocal sw=2 sts=2 ts=2 et
autocmd MyAutoCmd Syntax markdown syntax sync fromstart
autocmd MyAutoCmd FileType markdown nnoremap <buffer><silent><Leader>= :<C-u>call append('.', repeat('=', strdisplaywidth(getline('.'))))<CR>
autocmd MyAutoCmd FileType markdown nnoremap <buffer><silent><Leader>- :<C-u>call append('.', repeat('-', strdisplaywidth(getline('.'))))<CR>


autocmd MyAutoCmd FileType html setlocal sw=2 sts=2 ts=2 et
autocmd MyAutoCmd FileType yaml setlocal sw=2 sts=2 ts=2 et
autocmd MyAutoCmd FileType javascript setlocal sw=2 sts=2 ts=2 et
autocmd MyAutoCmd FileType less setlocal sw=2 sts=2 ts=2 et

autocmd MyAutoCmd FileType c   setlocal foldmethod=syntax
autocmd MyAutoCmd FileType cpp setlocal foldmethod=syntax

autocmd MyAutoCmd FileType ruby setlocal sw=2 sts=2 ts=2 et
autocmd MyAutoCmd FileType vim  setlocal sw=2 sts=2 ts=2 et

autocmd MyAutoCmd BufRead,BufNewFile *.scala  set filetype=scala
autocmd MyAutoCmd BufRead,BufNewFile *.sbt set filetype=sbt
autocmd MyAutoCmd FileType scala setlocal foldmethod=syntax
autocmd MyAutoCmd FileType scala setlocal sw=2 sts=2 ts=2 et

set tags+=.tags

let g:netrw_liststyle=3     "When viewing directories, show nested tree mode
let g:netrw_dirhistmax = 0  "Don't create .netrwhist files
let g:netrw_winsize = 15

if isdirectory(expand('~/.vim/bundle/vim-airline'))
  set laststatus=2 " Always show statusbar
  let g:airline_detect_paste=1 " Show PASTE if in paste mode
endif

if isdirectory(expand('~/.vim/bundle/syntastic'))
  set statusline+=%#warningmsg#
  set statusline+=%{SyntasticStatuslineFlag()}
  set statusline+=%*

  let g:syntastic_always_populate_loc_list = 1
  let g:syntastic_auto_loc_list = 1
  let g:syntastic_check_on_open = 1
  let g:syntastic_check_on_wq = 0
endif

if isdirectory(expand('~/.vim/bundle/ack.vim'))
  if executable('ag')
    let g:ackprg = "ag --nocolor --nogroup --column"
  elseif executable('ack-grep')
    let g:ackprg = "ack-grep --nocolor --nogroup --column"
  elseif executable('ack')
    let g:ackprg = "ack --nocolor --nogroup --column"
  endif
endif

if isdirectory(expand('~/.vim/bundle/tagbar'))
  nnoremap <silent> <leader>tt :TagbarToggle<CR>
endif

if isdirectory(expand('~/.vim/bundle/vim-session'))
  let g:session_autoload        = 'yes'
  let g:session_autosave        = 'yes'
  let g:session_default_to_last = 'yes'
  let g:session_directory = expand('$CACHE/vimsessions')
endif

if isdirectory(expand('~/.vim/bundle/ctrlp.vim'))
  nnoremap <silent> <M-r> :CtrlPMRU<CR>
  nnoremap <silent> <M-b> :CtrlPBuffer<CR>
  let g:ctrlp_working_path_mode = 'raw'
  let g:ctrlp_custom_ignore = {
        \ 'dir':  '\.git$\|\.hg$\|\.svn$',
        \ 'file': '\.exe$\|\.so$\|\.dll$\|\.pyc$' }

  " On Windows use "dir" as fallback command.
  if IsWindows()
    let s:ctrlp_fallback = 'dir %s /-n /b /s /a-d'
  elseif executable('ag')
    let s:ctrlp_fallback = 'ag %s --nocolor -l -g ""'
  elseif executable('ack-grep')
    let s:ctrlp_fallback = 'ack-grep %s --nocolor -f'
  elseif executable('ack')
    let s:ctrlp_fallback = 'ack %s --nocolor -f'
  else
    let s:ctrlp_fallback = 'find %s -type f'
  endif
  let g:ctrlp_user_command = {
        \ 'types': {
        \ 1: ['.git', 'cd %s && git ls-files . --cached --exclude-standard --others'],
        \ 2: ['.hg', 'hg --cwd %s locate -I .'],
        \ },
        \ 'fallback': s:ctrlp_fallback
        \ }

  if isdirectory(expand('~/.vim/bundle/ctrlp-funky'))
    " CtrlP extensions
    let g:ctrlp_extensions = ['funky']
  endif
endif

if isdirectory(expand('~/.vim/bundle/YouCompleteMe'))
  let g:ycm_autoclose_preview_window_after_completion = 1
  let g:ycm_min_num_identifier_candidate_chars = 4
  ""  let g:ycm_extra_conf_globlist = ['~/repos/*']
  ""  let g:ycm_filetype_specific_completion_to_disable = {'javascript': 1}
  " remap Ultisnips for compatibility for YCM

  let g:UltiSnipsExpandTrigger="<c-j>"
  let g:UltiSnipsJumpForwardTrigger="<c-j>"
  let g:UltiSnipsJumpBackwardTrigger="<c-k>"

  nnoremap <leader>y :YcmForceCompileAndDiagnostics<cr>
  nnoremap <leader>tg :YcmCompleter GoTo<CR>
  nnoremap <leader>td :YcmCompleter GoToDefinition<CR>
  nnoremap <leader>tc :YcmCompleter GoToDeclaration<CR>
endif

if isdirectory(expand('~/.vim/bundle/tern_for_vim'))

endif

if isdirectory(expand('~/.vim/bundle/vim-javascript'))
  let g:javascript_enable_domhtmlcss = 1
endif
